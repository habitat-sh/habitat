# TODO: Pass in an argument for the Docker image

# Verify that environment variables prefixed with HAB_STUDIO_SECRET_ do
# in fact make it into an interactive Docker studio.

exit -onexit {
    # Be sure to always shut the Studio down; otherwise we'll leave
    # the container running.
    send "exit\r"
    wait 2
}

# Define this as a variable for convenience and readability.
set studio_prompt "default:/src:0\]# "

# Don't colorize output, otherwise we need to account for ANSI color
# codes in our `expect` matches.
set env(HAB_NOCOLORING) true
set env(HAB_STUDIO_SECRET_FOO) monkeys

spawn hab studio enter -D

expect {
    "Exported: FOO=\\\[redacted\\\]" {}
    timeout {
        fail "Did not see redacted variable in output"
    }
}

expect $studio_prompt
send "echo \$FOO\r"

# We don't want to wait too long for a failing case
set timeout 3

# TODO (CM): This general pattern could be extracted as an assert
# library function
expect {
    "monkeys" {}
    timeout {
        fail "Did not find secret environment variable FOO"
    }
}
expect $studio_prompt
pass "Found secret environment variable FOO"
